#!/usr/bin/perl
use Cwd;
use File::Path;

sub update_diffs($$);

use constant {
    MAXKEEP => 60
};

umask 0002;
$| = 1;
use vars '$DB::inhibit_exit';
$DB::inhibit_exit = 0;

my $TMP = "/sourceware/snapshot-tmp/cygwin/$$";
my $http = "http://cygwin.org/snapshots";
my $wwwdir = getcwd;

open(TEMPLATE, "master.template") || die("master.template: $!");
my $template = join('', <TEMPLATE>);
close(TEMPLATE);

open(PERDAY, "perday.template") || die("perday.template: $!");
my $perday = join('', <PERDAY>);
close(PERDAY);

open(PERDAYNC, "perdaync.template") || die("perdaync.template: $!");
my $perdaync = join('', <PERDAYNC>);
close(PERDAYNC);

open(DIFFT, "diff.template") || die("diff.template: $!");
my $difft = join('', <DIFFT>);
close(DIFFT);

open(CLT, "changelog.template") || die("changelog.template: $!");
my $changelogt = join('', <CLT>);
close(CLT);

my $cwd = getcwd;
chdir $wwwdir or die "couldn't cd to $wwwdir - $!\n";
opendir D, '.';
my %bydate = ();
my @snaps = ();
for (reverse sort readdir D) {
    next unless /^(.*?)-(\d\d\d\d\d\d\d\d)\.(.*)$/o;
    push @{$bydate{$2}}, $_;
    push(@snaps, $2) if @snaps < MAXKEEP && $1 eq 'winsup-src' && $3 eq 'tar.bz2';
}
closedir D;

my @month = ('', '01', '02', '03', '04', '05', '06', '07', '08',
	     '09', '10', '11', '12');

my %var;
for $i (0..$#snaps) {
    update_diffs('winsup', $i);
    my $date = $snaps[$i];
    $var{'date'} = $date;
    ($var{'longdate'} = $date) =~ s/(....)(..)(..)/$1-$month[$2]-$3/;
    my ($ss, $mm, $hh, $mday, $m, $yr) = (gmtime(time))[0..5];
    $var{'longdate'} .= sprintf ' %02d:%02d:%02d UTC', $hh, $mm, $ss;
    ($var{'longprev'} = $snaps[$i+1]) =~ s/(....)(..)(..)/$1-$month[$2]-$3/;
    if ($i < $#snaps) {
	$var{'prev'} = $snaps[$i+1];
	$var{'wsdfsz'} = &size("winsup-diffs-$snaps[$i+1]-$date");
	$var{'wsclsz'} = &size("winsup-changelog-$snaps[$i+1]-$date");
	$var{'changelog'} = $changelogt;
	0 while $var{'changelog'} =~ s/<var ([a-z]+)>/$var{$1}/ge;
	$var{'diffs'} = $difft;
	0 while $var{'diffs'} =~ s/<var ([a-z]+)>/$var{$1}/ge;
    } else {
	$var{'changelog'} = '';
	$var{'diffs'} = '';
    }

    $var{'xfiles'} = file_row("winsup-src-$snaps[$i].tar.bz2");
    $var{'xfiles'} .= file_row("cygwin1-$snaps[$i].dll.bz2");
    $var{'xfiles'} .= file_row("cygwin1-$snaps[$i].dbg.bz2");
    $var{'xfiles'} .= file_row("cygwin-inst-$snaps[$i].tar.bz2");
    $var{'xfiles'} .= file_row("cygwin-src-$snaps[$i].tar.bz2");

    if ($i == $#snaps || (stat("winsup-diffs-$snaps[$i+1]-$date"))[7] > 0) {
	$t = $perday;
    } else {
	$t = $perdaync;
    }
    0 while $t =~ s/<var ([a-z]+)>/$var{$1}/ge;
    $perdays .= $t;
}

rmtree $TMP, 0, 0;

$var{'date'} = $snaps[0];
($var{'longdate'} = $snaps[0]) =~ s/(....)(..)(..)/$1-$month[$2]-$3/;
$var{'perday'} = $perdays;
0 while $template =~ s/<var ([a-z]+)>/$var{$1}/ge;

open INDEX, '>', 'index.html';
print INDEX $template;
chmod 0775, \*INDEX;
close INDEX;

my @tounlink = reverse sort keys %bydate;
for (my $i = MAXKEEP; $i < @tounlink; $i++) {
    unlink @{$bydate{$tounlink[$i]}};
}

exit(0);

sub file_row {
    my $f = shift;
    return "" unless -f $f;
    local($sz) = &size($f);
    my $rv = "<tr>";
    $rv .= "<td><tt>&nbsp; $f</tt></td>";
    $rv .= "<td align=right colspan=3>$sz Kb</td>";
    if ($f !~ /^(.*\.tar)\.bz2$/o) {
	$rv .= "<td align=center><br></td>";
    } else {
	$rv .= "<td align=center><a href=\"$1\">list</a></td>";
	if (!-e $1) {
	    open my $fd, '>', $1;
	    chmod 0664, $fd;
	    close $fd;
	}
    }
    $rv .= "<td align=center><a href=\"$http/$f\">download</a></td>\n";
    $rv .= "</tr>\n";
    return $rv;
}

sub size {
    print "No file $_[0]\n" unless -f $_[0];
    $sz = int(((stat($_[0]))[7]+1023) / 1024);
    0 while $sz =~ s/(.*\d)(\d\d\d)/$1,$2/;
    return $sz;
}

sub mtime {
    if ( -f $_[0]) {
	return (stat(_))[9];
    } else {
	return 0;
    }
}

sub update_diffs($$) {
    my $base = shift;
    my $i = shift;

    my $basesnap = $snaps[$i + 1] or return;
    my $nextsnap = $snaps[$i];
    my $basesrc = "$base-src-$basesnap";
    my $nextsrc = "$base-src-$nextsnap";

    my $sf = &mtime("$basesrc.tar.bz2");
    my $osf = &mtime("$nextsrc.tar.bz2");
    my $df = &mtime("$base-diffs-$basesnap-$nextsnap");
    my $cl = &mtime("$base-changelog-$basesnap-$nextsnap");

    if (!$df || $df < $sf || $df < $osf) {
	print "build $base-diffs-$basesnap-$nextsnap\n";
	my $basedir = need($basesnap, $basesrc);
	my $nextdir = need($nextsnap, $nextsrc);
	
	open OUT, '>', "$base-diffs-$basesnap-$nextsnap" or die "$0: couldn't open $base-diffs-$basesnap-$nextsnap for output - $!\n";
	open DIFF, '-|', "cd $TMP; exec diff -p -N -d -r -U2 $basedir $nextdir" or die "$0: diff $basedir $nextdir failed - $!\n";
	print OUT "Differences between $basesnap and $nextsnap\n\n";
	my $keeper = 0;
	my $sawstuff = 0;
	while (<DIFF>) {
	    $keeper = !/\.snapshot-date|ChangeLog/o if /^diff /o;
	    print OUT if $keeper;
	    $sawstuff ||= $keeper;
	}
	close DIFF;
	chmod 0664, \*OUT;
	close OUT;
	unlink "$base-diffs-$basesnap-$nextsnap" unless $sawstuff;
    }

    if (!$cl || $cl < $sf || $cl < $osf) {
	print "build $base-changelog-$basesnap-$nextsnap\n";
	my $basedir = need($basesnap, $basesrc);
	my $nextdir = need($nextsnap, $nextsrc);
	open OUT, '>', "$base-changelog-$basesnap-$nextsnap" or die "$0: couldn't open $base-changelog-$basesnap-$nextsnap for writing - $!\n";
	print OUT "ChangeLog for $basesnap to $nextsnap\n\n";
	open FIND, '-|', "cd $TMP/$basedir; exec find . -name ChangeLog -print" or die "$0: 'find -name Changelog -print' failed - $!\n";
	while ($f = <FIND>) {
	    $f =~ s/[\r\n]+$//o;
#	    $f =~ s!cygwin-snapshot-\d+-1/!!o;
	    open(DIFF, "diff $TMP/$basedir/$f $TMP/$nextdir/$f |");
	    while (<DIFF>) {
		last if /^> /;
	    }
	    if (/^> /) {
		print OUT "\n\n---------- $f ----------\n\n";
		s/^\> //;
		print OUT;
		while (<DIFF>) {
		    last unless /^> /;
		    s/^\> //;
		    print OUT;
		}
	    }
	    close DIFF;
	}
	close FIND;
	chmod 0664, \*OUT;
	close OUT;
    }
}

sub need {
    my($snap, $tbz) = @_;
    if (!$::unpacked{$tbz}) {
	print "unpacking $tbz\n";
	$::unpacked{$tbz} = 1;
	mkdir($TMP, 0755) unless -d $TMP;
	mkdir("$TMP/$tbz", 0755);
	system '/bin/tar', 'xjf', "$tbz.tar.bz2", '-C', "$TMP/$tbz";
    }
    return -d "$TMP/$tbz/cygwin-snapshot-$snap-1" ?
	      "$tbz/cygwin-snapshot-$snap-1" : $tbz;
}
